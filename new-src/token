/*!
 * Copyright (c) 2018 Aaron Delasy
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

obj Token {
  type: int
  val: str
  start: int
  end: int

  fn Token_str (self: Self) str {
    val := self._valEscaped()
    loc := "(" + self.start.str() + "-" + self.end.str() + ")"

    return tkToStr(self.type).slice(3) + loc + (val == "" ? "" : ": " + val)
  }

  fn Token_xml (self: Self) str {
    return
      "<Token" +
      " type=\"" + tkToStr(self.type).slice(3) + "\"" +
      " val=\"" + self._valEscaped(true) + "\"" +
      " start=\"" + self.start.str() + "\"" +
      " end=\"" + self.end.str() + "\"" +
      " />"
  }

  fn _valEscaped (self: Self, xml := false) str {
    mut result := ""

    loop i := 0; i < self.val.len; i++ {
      ch := self.val[i]

      if ch == '\f' {
        result += "\\f"
      } elif ch == '\n' {
        result += "\\n"
      } elif ch == '\r' {
        result += "\\r"
      } elif ch == '\t' {
        result += "\\t"
      } elif ch == '\v' {
        result += "\\v"
      } elif ch == '"' && xml {
        result += "\\\""
      } else {
        result += ch.str()
      }
    }

    return result
  }
}

fn Token_isCharEsc (ch: char) bool {
  return
    ch == '0' ||
    ch == 'b' ||
    ch == 'f' ||
    ch == 'n' ||
    ch == 'r' ||
    ch == 't' ||
    ch == 'v' ||
    ch == '"' ||
    ch == '\'' ||
    ch == '\\'
}

fn Token_isId (ch: char) bool {
  return ch.isAlphaNum() || ch == '_'
}

fn Token_isIdStart (ch: char) bool {
  return ch.isAlpha() || ch == '_'
}

fn Token_isIntBin (ch: char) bool {
  return ch == '0' || ch == '1'
}

fn Token_isIntDec (ch: char) bool {
  return ch.isDigit()
}

fn Token_isIntHex (ch: char) bool {
  return
    ch.isDigit() ||
    ch == 'A' || ch == 'a' ||
    ch == 'B' || ch == 'b' ||
    ch == 'C' || ch == 'c' ||
    ch == 'D' || ch == 'd' ||
    ch == 'E' || ch == 'e' ||
    ch == 'F' || ch == 'f'
}

fn Token_isIntOct (ch: char) bool {
  return
    ch == '0' ||
    ch == '1' ||
    ch == '2' ||
    ch == '3' ||
    ch == '4' ||
    ch == '5' ||
    ch == '6' ||
    ch == '7'
}

fn Token_isNotNewLine (ch: char) bool {
  return ch != '\n'
}

fn Token_isStrEsc (ch: char) bool {
  return Token_isCharEsc(ch) || ch == '{'
}

fn Token_isWhitespace (ch: char) bool {
  return ch == ' ' ||
    ch == '\f' ||
    ch == '\n' ||
    ch == '\r' ||
    ch == '\t' ||
    ch == '\v'
}
